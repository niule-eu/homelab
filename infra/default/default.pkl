extends "@fcos_butane/ButaneFcos.pkl"
import "@fcos_butane/Storage.pkl"

local dir_parts = List(".config", "systemd", "user", "sockets.target.wants")
local dirs = dir_parts.foldIndexed(
  Map(), 
  (i, acc, elem) -> (
    let (name = "/home/core/\(dir_parts.take(i + 1).join("/"))")
      Map(name, new Storage.Directory {
        group { id = 1000 }
        user { id = 1000 }
        mode { value = "0o755" }
        path = name
      }) + acc
  )
)
.toMapping()

passwd {
  users {
    ["core"] {
      name = "core"
      uid = 1000
      groups {
        "sudo"
        "wheel"
      }
      when (core_password_hash != null) {
        password_hash = core_password_hash
      }
      ssh_authorized_keys {
        "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIC9AaghNePDNxI//W6OX6Yp6gssMpDUzK+4XoUiXnRZE fedora-sway-atomic@niuleh.eu"
      }
    }
  }
}

systemd {
  units {
    ["docker.service"] {
      name = "docker.service"
      mask = true
    }
    ["docker.socket"] {
      name = "docker.socket"
      mask = true
    }
  }
}

storage {
  links {
    when (enable_linger == true) {
      ["/home/core/.config/systemd/user/sockets.target.wants/podman.socket"] {
        path = "/home/core/.config/systemd/user/sockets.target.wants/podman.socket"
        target = "/usr/lib/systemd/user/podman.socket"
        user {
          id = 1000
        }
        group {
          id = 1000
        }
        hard = false
      }
    }
    when (enable_rootful_podman_socket == true) {
      ["/etc/systemd/system/sockets.target.wants/podman.socket"] {
        path = "/etc/systemd/system/sockets.target.wants/podman.socket"
        target = "/usr/lib/systemd/podman.socket"
        user {
          id = 0
        }
        group {
          id = 0
        }
        hard = false
      }
    }
  }
  when (enable_linger) {
    directories {
      ...dirs
    }
  }
  files {
    when (enable_linger == true) {
      ["/var/lib/systemd/linger/core"] {
        path = "/var/lib/systemd/linger/core"
        user {
          id = 0
        }
        group {
          id = 0
        }
        mode {
          value = "0o644"
        }
        contents {
          inline = ""
        }
      }
    }
    when (hostname != null) {
      ["/etc/hostname"] {
        path = "/etc/hostname"
        mode {
          value = "0o644"
        }
        overwrite = true
        contents {
          inline = hostname
        }
      }
    }
    
    when (disable_stub_listener) {
      ["/etc/systemd/resolved.conf.d/disable-stub.conf"] {
        path = "/etc/systemd/resolved.conf.d/disable-stub.conf"
        mode {
          value = "0o0644"
        }
        contents {
          inline = read(path.drop(1)).text
        }
      }
    }

    when (nm_no_auto_default.length > 0) {
      ["/etc/NetworkManager/conf.d/no-auto-default.conf"] {
        path = "/etc/NetworkManager/conf.d/no-auto-default.conf"
        mode {
          value = "0o644"
        }
        contents {
          inline = 
            """
            [main]
            no-auto-default=\(nm_no_auto_default.join(","))
            """
        }
    }
    }
  }
  
}

hidden hostname : String? = null 
hidden disable_stub_listener : Boolean = false
hidden core_password_hash : String? = null
hidden nm_no_auto_default : Listing<String> = new Listing {}
hidden enable_linger : Boolean = false
hidden enable_rootful_podman_socket = false
