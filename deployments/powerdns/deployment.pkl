import "package://pkg.pkl-lang.org/pkl-k8s/k8s@1.2.1#/K8sResource.pkl"
import "package://pkg.pkl-lang.org/pkl-k8s/k8s@1.2.1#/api/apps/v1/Deployment.pkl"
import "configmaps/pdns-sqlite3-schema.pkl" as pdnssqlite3schema
import "configmaps/pdns-config.pkl" as pdnsconfig

hidden config_maps =
  let (pdns_config = (pdnsconfig.resources.toList()))
  let (pdns_sqlite3_schema = (pdnssqlite3schema.resources.toList()))
    (pdns_config + pdns_sqlite3_schema).toListing()

resources: Listing<K8sResource> = (config_maps) {
  new Deployment {
    metadata {
      name = "pdns"
    }
    spec {
      replicas = 1
      selector {
        matchLabels {
          ["app"] = "pdns"
        }
      }
      strategy {
        type = "Recreate"
      }
      template {
        metadata {
          labels {
            ["app"] = "pdns"
          }
        }
        spec {
          initContainers {
            new {
              name = "sqlite3-init"
              image = "docker.io/powerdns/pdns-auth-master:latest"
              command {
                "sqlite3"
              }
              args {
                "-init"
                "/tmp/schema.sql"
                "/var/lib/powerdns/pdns.sqlite3"
              }
              volumeMounts {
                new {
                  mountPath = "/var/lib/powerdns/:Z"
                  name = "pdns-sqlite3"
                }
                new {
                  mountPath = "/tmp/schema.sql:Z"
                  name = "pdns-sqlite3-schema"
                  subPath = "schema.sql"
                }
              }
            }
          }
          containers {
            new {
              name = "auth"
              image = "docker.io/powerdns/pdns-auth-master:latest"
              ports {
                new {
                  containerPort = 5300
                  hostIP = "127.0.0.1"
                  hostPort = 5300
                  protocol = "TCP"
                }
                new {
                  containerPort = 5300
                  hostIP = "127.0.0.1"
                  hostPort = 5300
                  protocol = "UDP"
                }
                new {
                  containerPort = 8081
                  hostIP = "127.0.0.1"
                  hostPort = 8081
                  protocol = "TCP"
                }
              }
              volumeMounts {
                new {
                  mountPath = "/var/lib/powerdns/:Z"
                  name = "pdns-sqlite3"
                }
                new {
                  mountPath = "/etc/powerdns/pdns.d/:Z"
                  name = "pdns-config"
                }
              }
            }
          }
          volumes {
            new {
              name = "pdns-sqlite3"
              persistentVolumeClaim {
                claimName = "pdns-sqlite3"
              }
            }
            new {
              name = "pdns-sqlite3-schema"
              configMap {
                name = "pdns-sqlite3-schema"
              }
            }
            new {
              name = "pdns-config"
              configMap {
                name = "pdns-config"
              }
            }
          }
        }
      }
    }
  }
}

output {
  value = resources
  renderer = (K8sResource.output.renderer as YamlRenderer) {
    isStream = true
  }
}