open module template

import "@k8s/K8sResource.pkl"
import "@k8s/K8sObject.pkl"
import "@k8s/api/core/v1/PodSpec.pkl"
import "@k8s/api/core/v1/Volume.pkl"
import "@k8s/api/core/v1/ConfigMap.pkl"
import "@k8s/api/core/v1/Secret.pkl"
import "@k8s/api/apps/v1/Deployment.pkl"

configMaps : Mapping<K8sObject.Rfc1035Label, ConfigMap> = resourceMapping(ConfigMap)

secrets : Mapping<K8sObject.Rfc1035Label, Secret> = resourceMapping(Secret)

hidden initContainers : Mapping<K8sObject.Rfc1035Label, PodSpec.Container> = nameMapping(PodSpec.Container)

hidden containers : Mapping<K8sObject.Rfc1035Label, PodSpec.Container> = nameMapping(PodSpec.Container)

hidden volumes : Mapping<K8sObject.Rfc1035Label, Volume> = nameMapping(Volume)

hidden podName : K8sObject.Rfc1035Label?

function nameMapping(type) : Mapping<String, unknown> =
  new Mapping { default = (key) -> (type) { name = key}}

function resourceMapping(type): Mapping<String, unknown> =
  new Mapping { default = (key) -> (type) { metadata { name = key } } }

deployment : Mapping<K8sObject.Rfc1035Label, Deployment> = new {
  [podName!!] {
    metadata {
      name = podName!!
    }
    spec {
      replicas = 1
      strategy {
        type = "Recreate"
      }
      selector {
        matchLabels {
          ["app"] = podName!!
        }
      }
      template {
        metadata {
          labels {
            ["app"] = podName!!
          }
          annotations {
            ["io.podman.annotations.infra.name"] = "\(podName!!)-infra"
          }
        }
        spec {
          initContainers = module.initContainers.toMap().values.toListing()
          containers = module.containers.toMap().values.toListing()
          volumes = module.volumes.toMap().values.toListing()
        }
      }
    }
  }
}

output {
  renderer = new YamlRenderer {
    isStream = true
    converters = (K8sObject.output.renderer as YamlRenderer).converters
  }
  value = module.toMap().values.flatMap((item) -> item.toMap().values)
}